plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.roderly'
version = '0.0.1-SNAPSHOT'

def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.0'
	implementation 'com.auth0:java-jwt:4.4.0'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'com.mysql:mysql-connector-j'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	compileOnly("org.projectlombok:lombok")

	annotationProcessor("org.projectlombok:lombok")
}

tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder-jammy-base:latest'
}


tasks.register('npmInstall', Exec) {
	workingDir = 'src/front'
	if (isWindows) {
		commandLine 'npm.cmd', 'install'
	} else {
		commandLine 'npm', 'install'
	}
}

tasks.register('npmBuild', Exec) {
	dependsOn 'npmInstall'
	workingDir = 'src/front'
	if (isWindows) {
		commandLine 'npm.cmd', 'run', 'build'
	} else {
		commandLine 'npm', 'run', 'build'
	}
}


tasks.register('buildWithFrontend') {
	dependsOn 'npmBuild'
	finalizedBy 'clean', 'build'
}


tasks.named('buildWithFrontend') {
	doLast {
		println 'Front-end and back-end build complete!'
	}
}


tasks.register('runDevMode', Exec) {
	workingDir = 'src/front'
	commandLine 'npm.cmd', 'run', 'serve'
}


tasks.register('runProductionJar', Exec) {
	workingDir = 'build/libs'
	commandLine 'java', '-jar', 'microbiome-lab-ufu-0.0.1-SNAPSHOT.jar', '-tf'
}



//Executar o npm em modo de desenvolvmento
// ./gradlew runDevMode

//Para Builder o Project
// ./gradlew buildWithFrontend

//Para Executar o arquivo Jar de produção
//./gradlew runProductionJar



